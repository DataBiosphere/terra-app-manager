dependencies {
    implementation 'io.swagger.core.v3:swagger-annotations'
    runtimeOnly 'org.webjars.npm:swagger-ui-dist:4.9.0'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli'

    // Versioned by Spring:
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'org.webjars:webjars-locator-core'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

def artifactGroup = "${group}.appmanager"

generateSwaggerCode {
    inputFile = file('src/main/resources/api/openapi.yml')
    language = 'spring'
    components = ['models', 'apis']
    additionalProperties = [
        apiPackage       : "${artifactGroup}.api",
        modelPackage     : "${artifactGroup}.api.model",
        dateLibrary      : 'java11',
        java8            : true,
        interfaceOnly    : 'true',
        useTags          : 'true',
        jakarta          : 'true',
        springBootVersion: dependencyManagement.managedVersions['org.springframework.boot:spring-boot']
    ]
}

String swaggerOutputSrc = "${generateSwaggerCode.outputDir}/src/main/java"

idea.module.generatedSourceDirs = [file(swaggerOutputSrc)]
sourceSets.main.java.srcDir swaggerOutputSrc
compileJava.dependsOn generateSwaggerCode

// see https://github.com/n0mer/gradle-git-properties
gitProperties {
    keys = []
    customProperty('appmanager.version.gitTag', { it.describe(tags: true) })
    customProperty('appmanager.version.gitHash', { it.head().abbreviatedId })
    customProperty('appmanager.version.github', { "https://github.com/DataBiosphere/service/terra-app-manager/tree/${it.describe(tags: true)}" })
    customProperty('appmanager.version.build', version)
}
