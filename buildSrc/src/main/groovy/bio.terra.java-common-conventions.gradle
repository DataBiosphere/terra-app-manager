plugins {
    id 'idea'
    id 'jacoco'
    id 'java'

    id 'com.diffplug.spotless'
    id 'org.hidetake.swagger.generator'
}

boolean isCiServer = System.getenv().containsKey("CI")

if (!isCiServer) {
    tasks.withType(JavaExec).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
    tasks.withType(Test).configureEach {
        systemProperty 'spring.profiles.include', 'human-readable-logging'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    maven {
        // Terra proxy for maven central
        url 'https://broadinstitute.jfrog.io/broadinstitute/maven-central/'
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

// Spring Boot 3.2.4 pulls in opentelemetry-bom 1.31.0.
// It must have version >= 1.34.1 for compatibility with terra-common-lib 1.1.11:
ext['opentelemetry.version'] = '1.36.0'
// Spring Boot 3.2.4 pulls in io.netty:netty-bom 4.1.107.Final which is impacted by CVE-2024-29025.
ext['netty.version'] = '4.1.108.Final'

dependencies {
    // this is necessary for a transitive dependency
    // preference is to update terra-common-lib to address transitive dependencies
    // TODO: remove this when transitive dependencies are addressed
//    implementation 'org.yaml:snakeyaml:2.0'
//    // transitive dependency of:
//    // - org.broadinstitute.dsde.workbench:sam-client_2.13:0.1-5281c21
////    implementation 'org.json:json:20240303'
//    // transitive dependency of:
//    // - (multiple places)
//    implementation 'ch.qos.logback:logback-core:1.4.9'
//    implementation 'ch.qos.logback:logback-classic:1.4.9'

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.0'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.47'

    implementation 'org.slf4j:slf4j-api'

    testImplementation 'org.hamcrest:hamcrest:2.2'

    implementation 'io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:2.2.0'

    // this is a necessity of terra-common-lib:1.1.11
    implementation 'com.google.guava:guava:33.2.0-jre'

    implementation 'bio.terra:terra-common-lib:1.1.11-SNAPSHOT'
    implementation 'bio.terra:datarepo-client:1.349.0-SNAPSHOT'
}

tasks.named('test') {
    useJUnitPlatform()
}

version = gradle.releaseVersion
group = 'bio.terra'

spotless {
    java {
        targetExclude "${buildDir}/**"
        targetExclude "**/swagger-code/**"
        googleJavaFormat()
    }
}

// Run spotless check when running in github actions, otherwise run spotless apply.
compileJava {
    if (isCiServer) {
        dependsOn(spotlessCheck)
    } else {
        dependsOn(spotlessApply)
    }
}

jacocoTestReport {
    reports {
        // sonar requires XML coverage output to upload coverage data
        xml.required = true
    }
}
